# Main loop
main: fn
    \ size_x: Number, size_y: Number ->
        size_x
        size_y
        tile_field_size
        vec_store
        init_frame_count
        init
        main_inner
end

main_inner: fn
    \ ->
        draw
        count_frame
        update
        main_inner
end

# Draw
draw: fn
    \ ->
        clear_pixels
        draw_snake
        draw_food
        # This blocks until the display system is ready to process the
        # next frame.
        submit_frame
end

# Draw - clear pixels
clear_pixels: fn
    \ ->
        init_tile_index clear_pixels_inner
end

clear_pixels_inner: fn
    \ tile_x, tile_y ->
        # This is a recursive function, so we might have been at
        # it for a while, if we make it here. Check if the tile
        # index has gone beyond the last tile, which would let
        # us know that we're done.
        tile_y
        check_tile_index
        fn
            \ 0 ->
                # Done.

            \ _ ->
                # Apparently we're not done yet.
                tile_x
                tile_y
                0
                0
                0
                255
                set_pixel
                tile_x
                tile_y
                increment_tile_index
                clear_pixels_inner
        end
            eval
end

draw_snake: fn
    \ ->
        0 _draw_snake_inner
end

_draw_snake_inner: fn
    \ index ->
        positions
        vec_buf_len
        index
        greater_s8
        fn
            \ index_is_within_bounds ->
                index_is_within_bounds
                index
                _draw_snake_draw_rest_of_snake
        end
            eval
end

_draw_snake_draw_rest_of_snake: fn
    \ 0, _ ->
        # Index is out of bounds. We're done.

    \ 1, index ->
        # Index is valid. Continue drawing the snake.
        index
        _draw_snake_draw_body_segment
        index
        1
        add_u8
        _draw_snake_inner
end

_draw_snake_draw_body_segment: fn
    \ index ->
        positions
        index
        vec_buf_get
        _draw_snake_body_color
        set_pixel
end

_draw_snake_body_color: fn
    \ ->
        0 255 0 255
end

draw_food: fn
    \ ->
        food_position
        vec_load
        255
        0
        0
        255
        set_pixel
end

# Draw - write tiles - tile index
init_tile_index: fn
    \ ->
        0 0
end

check_tile_index: fn
    \ tile_y ->
        tile_field_size
        vec_load
        vec_y
        tile_y
        # Leave zero, if the y-coordinate has advanced beyond the last
        # line of the tile field. Otherwise, leave non-zero value.
        sub_s32
end

increment_tile_index: fn
    \ tile_x, tile_y ->
        tile_x
        _increment_tile_index_increment_coord
        _increment_tile_index_is_tile_x_within_limit
        _increment_tile_index_reset_x_if_overflowed
        tile_y
        _increment_tile_index_increment_y_if_necessary
end

_increment_tile_index_increment_coord: fn
    \ coord ->
        coord 1 add_u8
end

_increment_tile_index_is_tile_x_within_limit: fn
    \ tile_x ->
        tile_field_size
        vec_load
        vec_x
        tile_x
        greater_u8
        fn
            \ tile_x_within_limit ->
                tile_x
                tile_x_within_limit
        end
            eval
end

_increment_tile_index_reset_x_if_overflowed: fn
    \ _, 0 ->
        0 0

    \ tile_x, 1 ->
        tile_x 1
end

_increment_tile_index_increment_y_if_necessary: fn
    \ tile_x, 0, tile_y ->
        tile_x
        tile_y
        _increment_tile_index_increment_coord

    \ tile_x, 1, tile_y ->
        tile_x tile_y
end

# Tile field size
is_out_of_bounds: fn
    \ x, y ->
        tile_field_size
        vec_load
        fn \ limit_x, limit_y ->
            x
            limit_x
            _is_out_of_bounds_is_coord_within_bounds
            y
            limit_y
            _is_out_of_bounds_is_coord_within_bounds
            and
            not
        end
            eval
end

_is_out_of_bounds_is_coord_within_bounds: fn
    \ coord, limit ->
        coord
        0
        greater_u8
        limit
        coord
        greater_u8
        and
end

# Frame count
init_frame_count: fn
    \ ->
        1
        frame_count
        store
end

count_frame: fn
    \ ->
        # Grab the current frame count.
        frame_count
        load
        # Increment the frame count.
        1
        add_s32
        # Place a copy of the new frame count back where it came
        # from.
        copy
        frame_count
        store
        _count_frame_reset_frame_count_if_necessary
end

_count_frame_reset_frame_count_if_necessary: fn
    \ 121 ->
        init_frame_count

    \ _ ->
        # No reset necessary.
end

# Game state
init: fn
    \ ->
        init_should_game_run
        snake_init
        init_velocity
        init_next_position
        food_init
end

update: fn
    \ ->
        # The update logic does not run every frame.
        frame_count
        load
        2
        remainder_s32
        not
        should_game_run
        load
        and
        fn
            \ 1 ->
                read_input
                handle_input
                update_positions
                food_eat

            \ _ ->
                # Nothing to do.
        end
            eval
end

# Game state - should game run
init_should_game_run: fn
    \ ->
        1
        should_game_run
            store
end

# Game state - velocity
init_velocity: fn
    \ ->
        1
        0
        velocity
            vec_store
end

# Game state - next position
init_next_position: fn
    \ ->
        positions
        0
        vec_buf_get
        next_position
        vec_store
end

update_next_position: fn
    \ ->
        snake_head
        vec_x
        velocity
        vec_load
        vec_x
        add_s8
        snake_head
        vec_y
        velocity
        vec_load
        vec_y
        add_s8
        next_position
        vec_store
        next_position
        vec_load
        is_out_of_bounds
        fn
            \ 1 ->
                next_position
                vec_load
                fn \ next_x, next_y ->
                    tile_field_size
                    vec_load
                    fn \ limit_x, limit_y ->
                        next_x
                        limit_x
                        handle_coordinate_smaller_than_zero
                        fn \ next_x ->
                            next_y
                            limit_y
                            handle_coordinate_smaller_than_zero
                            fn \ next_y ->
                                next_x
                                limit_x
                                handle_coordinate_larger_than_limit
                                fn \ next_x ->
                                    next_y
                                    limit_y
                                    handle_coordinate_larger_than_limit
                                    fn \ next_y ->
                                        next_x
                                        next_y
                                        next_position
                                        vec_store
                                    end
                                        eval
                                end
                                    eval
                            end
                                eval
                        end
                            eval
                    end
                        eval
                end
                    eval

            \ _ ->
                # Not out of bounds. Nothing to do.
        end
            eval
end

handle_coordinate_smaller_than_zero: fn
    \ coord, limit ->
        0
        coord
        greater_s8
        fn \ coord_smaller_than_zero ->
            coord_smaller_than_zero
            fn
                \ 1 ->
                    coord limit add_s8

                \ _ ->
                    coord
            end
                eval
        end
            eval
end

handle_coordinate_larger_than_limit: fn
    \ coord, limit ->
        limit
        coord
        greater_u8
        fn \ limit_greater_than_coord ->
            limit_greater_than_coord
            fn
                \ 0 ->
                    coord
                    limit
                    sub_s32

                \ 1 ->
                    coord
            end
                eval
        end
            eval
end

# Game state - food
food_init: fn
    \ ->
        negatable_random
        abs
        tile_field_size
        vec_load
        vec_x
        remainder_s32
        negatable_random
        abs
        tile_field_size
        vec_load
        vec_y
        remainder_s32
        food_position
        vec_store
end

food_eat: fn
    \ ->
        _food_collides_with_snake
        fn
            \ 1 ->
                # The snake's head and the food are at the
                # same position.
                food_init
                grow_snake

            \ _ ->
        end
            eval
end

_food_collides_with_snake: fn
    \ ->
        snake_head
        food_position
        vec_load
        vec_eq
        fn \ head_collides ->
            food_position
            vec_load
            check_body_collision
            fn \ body_collides ->
                head_collides
                body_collides
                add_s32
                0
                greater_s32
            end
                eval
        end
            eval
end

# Game state - snake
snake_init: fn
    \ ->
        3
        snake_length
           store

        positions
            vec_buf_init

        positions
        15
        15
            vec_buf_push
end

snake_head: fn
    \ ->
        positions vec_buf_last
end

update_positions: fn
    \ ->
        update_next_position
        snake_head
        check_body_collision
        fn
            \ 0 ->
                positions
                next_position
                vec_load
                vec_buf_push
                pop_positions

            \ 1 ->
                # Collision. Do nothing.
        end
            eval
end

pop_positions: fn
    \ ->
        positions
        vec_buf_len
        snake_length
        load
        greater_s32
        fn
            \ 1 ->
                positions
                vec_buf_pop
                pop_positions

            \ _ ->
        end
            eval
end

grow_snake: fn
    \ ->
        snake_length
        load
        1
        add_s32
        fn \ snake_length_plus_growth ->
            snake_length_plus_growth
            positions
            vec_buf_capacity
            greater_s32
            fn
                \ 0 ->
                    snake_length_plus_growth
                    snake_length
                    store

                \ _ ->
            end
                eval
        end
            eval
end

check_body_collision: fn
    \ x, y ->
        x
        y
        0
        check_body_collision_inner
end

check_body_collision_inner: fn
    \ x, y, index ->
        positions
        vec_buf_len
        1
        sub_s32
        index
        greater_s32
        fn
            \ 1 ->
                positions
                index
                vec_buf_get
                vec_x
                x
                eq
                fn \ x_matches ->
                    positions
                    index
                    vec_buf_get
                    vec_y
                    y
                    eq
                    fn \ y_matches ->
                        x_matches
                        y_matches
                        add_s32
                        2
                        eq
                        fn
                            \ 0 ->
                                x
                                y
                                index
                                1
                                add_s32
                                check_body_collision_inner

                            \ _ ->
                                1
                        end
                            eval
                    end
                        eval
                end
                    eval

            \ _ ->
                0
        end
            eval
end

# Input
handle_input: fn
    \ 0 ->
        # No input available.

    \ 1 ->
        # up
        0
        -1
        s32_to_s8
        velocity
        vec_store

    \ 2 ->
        # left
        -1
        s32_to_s8
        0
        velocity
        vec_store

    \ 3 ->
        # down
        0 1 velocity vec_store

    \ 4 ->
        # right
        1 0 velocity vec_store

    \ _ ->
        # unexpected value
        brk
end

# Memory map
tile_field_size: fn
    \ ->
        0
end

frame_count: fn
    \ ->
        2
end

should_game_run: fn
    \ ->
        3
end

velocity: fn
    \ ->
        4
end

next_position: fn
    \ ->
        6
end

food_position: fn
    \ ->
        8
end

snake_length: fn
    \ ->
        10
end

positions: fn
    \ ->
        11
end

# Utilities - Vector
vec_x: fn
    \ x: Number, _: Number ->
        x
end

vec_y: fn
    \ _: Number, y: Number ->
        y
end

vec_load: fn
    \ address ->
        address
        load
        address
        1
        add_s32
        load
end

vec_store: fn
    \ x, y, address ->
        x: -> Number .
        address: -> Number .
        store
        y: -> Number .
        address
        1
        add_s32
        store
end

vec_copy: fn
    \ vx, vy ->
        vx: -> Number .
        vy: -> Number .
        vx
        vy
end

vec_drop: fn
    \ _, _ ->
end

vec_eq: fn
    \ ax, ay, bx, by ->
        ax
        bx
        eq
        ay
        by
        eq
        and
end

# Utilities - Vector Buffer
vec_buf_init: fn
    \ vec_buf ->
        0
        vec_buf _vec_buf_first
            store
        0
        vec_buf _vec_buf_next
            store
        64
        vec_buf _vec_buf_capacity
            store
end

vec_buf_get: fn
    \ vec_buf, index ->
        index
        2
        mul_u8_wrap
        fn \ offset ->
            vec_buf
            _vec_buf_first
            load
            fn \ base ->
                vec_buf
                base
                offset
                _vec_buf_address
                vec_load
            end
                eval
        end
            eval
end

vec_buf_last: fn
    \ vec_buf ->
        vec_buf
        vec_buf_len
        1
        sub_u8
        fn \ index ->
            vec_buf
            index
            vec_buf_get
        end
            eval
end

vec_buf_push: fn
    \ vec_buf, x, y ->
        vec_buf
        _vec_buf_next
        fn \ next_addr ->
            vec_buf
            next_addr
            load
            0
            _vec_buf_address
            fn \ address ->
                x
                y
                address
                vec_store
                next_addr: -> Number .
                _vec_buf_inc_index
            end
                eval
        end
            eval
end

vec_buf_pop: fn
    \ vec_buf ->
        vec_buf
        _vec_buf_first
        _vec_buf_inc_index
end

vec_buf_len: fn
    \ vec_buf ->
        vec_buf
        _vec_buf_first
        load
        fn \ first ->
            vec_buf
            _vec_buf_next
            load
            fn \ next ->
                next
                first
                sub_u8_wrap
                2
                div_u8
            end
                eval
        end
            eval
end

vec_buf_capacity: fn
    \ vec_buf ->
        vec_buf
        _vec_buf_capacity
        load
        2
        div_s32
end

_vec_buf_address: fn
    \ vec_buf, base, offset ->
        # Compute the memory address of a location within the
        # vector buffer.
        #
        # Takes two arguments:
        #
        # - `base`, which is an index into the buffer, as
        #   opposed to a real address. It can be larger than any
        #   actual address within the buffer.
        # - `offset`, which is the offset of the desired address
        #   `from base`.
        base: -> Number .
        offset: -> Number .
            add_u8_wrap

        vec_buf
            _vec_buf_capacity
            load

        remainder_s32

        vec_buf
            _vec_buf_buffer

        add_u8_wrap
end

_vec_buf_inc_index: fn
    \ index_addr ->
        index_addr: -> Number .
            load
        2
            add_u8_wrap

        index_addr

        store
end

_vec_buf_first: fn
    \ vec_buf ->
        vec_buf
        0
            add_s32
end

_vec_buf_next: fn
    \ vec_buf ->
        vec_buf
        1
            add_s32
end

_vec_buf_capacity: fn
    \ vec_buf ->
        vec_buf
        2
            add_s32
end

_vec_buf_buffer: fn
    \ vec_buf ->
        vec_buf: -> Number .
        3
            add_s32
end

# Utilities - Miscellaneous
negatable_random: fn
    \ ->
        read_random
        _negatable_random_return_or_continue
end

_negatable_random_return_or_continue: fn
    \ -2147483648 ->
        # Negating the minimum number would result in an
        # integer overflow.
        negatable_random

    \ random ->
        random: -> Number .
end

abs: fn
    \ v ->
        v
        v
        -1
        greater_s32
        fn
            \ v, 0 ->
                v
                    neg_s32

            \ v, _ ->
                v
        end
            eval
end
